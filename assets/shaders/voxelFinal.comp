//INCLUDES "voxelShared.comp"
#version 460
#define INV_GAMMA 0.4545 //for gamma correction (1 / 2.2)

layout(local_size_x = 16, local_size_y = 16) in; //16x16 local group size
layout(rgba32f, binding = 0) uniform image2D colorOutput; //the output texture

uniform vec3 camPos;    //the camera's position
uniform vec3 camDir;    //the direction the camera is pointing
uniform vec3 camPlaneU; //the vector that points along the x-axis of the screen
uniform vec3 camPlaneV; //the vector that points along the y-axis of the screen

uniform uint viewMode;  //what is displayed, 0 = lighting, 1 = depth, 2 = normals

uniform float ambientStrength; //the minimum light that every voxel receives

//--------------------------------------------------------------------------------------------------------------------------------//

void main()
{
	Voxel finalVoxel; //the final voxel that is hit
	Material finalMaterial;
	vec3 finalColorAdd;
	float finalColorMult;
	vec3 finalColor; //the final color that gets written to the texture

	//calculate ray position and direction:
	vec2 screenPos;
	screenPos.x = gl_GlobalInvocationID.x / (gl_NumWorkGroups.x * 8.0) - 1.0; //(0,0) at the center of the screen
	screenPos.y = gl_GlobalInvocationID.y / (gl_NumWorkGroups.y * 8.0) - 1.0;

	vec3 rayDir = normalize(camDir + screenPos.x * camPlaneU + screenPos.y * camPlaneV);
	vec3 invRayDir = 1.0 / rayDir;
	vec3 rayPos = camPos;

	//check if ray hits the map at all:
	vec2 intersection = intersect_AABB(invRayDir, rayPos, vec3(0.0), MAP_SIZE);

	if(intersection.x > intersection.y || intersection.y < 0) //make sky color if no intersection at all
	{
		finalColor = sky_color(rayDir);
	}
	else //if the map is hit, step through it
	{
		if(intersection.x > 0) //only increment rayPos if outside the box
			rayPos += rayDir * (intersection.x + EPSILON);

		if(step_map(rayDir, invRayDir, rayPos, false, true, finalVoxel, finalColorAdd, finalColorMult))
		{
			finalMaterial = materials[finalVoxel.material];
			finalVoxel.specLight *= finalMaterial.specular;
			finalVoxel.indirectLight *= 1.0 - finalMaterial.specular;
			finalVoxel.directLight *= 1.0 - finalMaterial.specular;

			switch(viewMode)
			{
			case 0: //total lighting
			{	
				vec3 solidColor;
				if(finalMaterial.emissive)
					solidColor = finalVoxel.albedo;
				else
					solidColor = (finalVoxel.directLight + finalVoxel.indirectLight + ambientStrength) * finalVoxel.albedo + finalVoxel.specLight;

				finalColor = pow(solidColor * finalColorMult + finalColorAdd, vec3(INV_GAMMA));
				break;
			}
			case 1: //albedo
			{
				finalColor = pow(finalVoxel.albedo, vec3(INV_GAMMA));
				break;
			}
			case 2: //direct light
			{
				finalColor = pow(finalMaterial.emissive == 1 ? finalVoxel.albedo : finalVoxel.directLight, vec3(INV_GAMMA));
				break;
			}
			case 3: //indirect light
			{
				finalColor = pow(finalMaterial.emissive == 1 ? finalVoxel.albedo : finalVoxel.indirectLight, vec3(INV_GAMMA));
				break;
			}
			case 4: //specular light
			{
				finalColor = pow(finalMaterial.emissive == 1 ? finalVoxel.albedo : finalVoxel.specLight, vec3(INV_GAMMA));
				break;
			}
			case 5: //normal
			{
				finalColor = vec3(abs(finalVoxel.normal));
				break;
			}
			}
		}
		else
		{
			finalColor = sky_color(rayDir);

			if(viewMode == 0)
				finalColor = finalColor * finalColorMult + finalColorAdd;
		}
	}

	//store final color to texture:
	ivec2 coords = ivec2(gl_GlobalInvocationID.xy);
	imageStore(colorOutput, coords, vec4(finalColor, 1.0));
}