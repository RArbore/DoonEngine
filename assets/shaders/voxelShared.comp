//----------------------------------------------------------BEGIN-SHARED----------------------------------------------------------//

#define CHUNK_SIZE ivec3(8, 8, 8) //size of each individual chunk
#define INV_CHUNK_SIZE vec3(0.125, 0.125, 0.125) //1 / CHUNK_SIZE, precomputed
#define HALF_INV_CHUNK_SIZE vec3(0.0625, 0.0625, 0.0625) //1 / 2 * CHUNK_SIZE, precomputed

#define EPSILON 0.0001 //to fix floating point error

uniform uvec3 mapSize;

//--------------------------------------------------------------------------------------------------------------------------------//

struct Voxel //a single voxel
{
	vec3 albedo;           //the "base color" of the voxel
	vec3 normal;           //the direction the voxel points (for lighting calculation)
	uint material;         //the index of this voxel's material

	vec3 indirectLight;    //the diffuse lighting of the voxel, caused by multiple light ray bounces
	float indirectSamples; //the cumulative number of diffuse lighting rays that have been cast for this voxel

	vec3 directLight;      //the direct lighting from the sun, creates shadows
	vec3 specLight;        //the lighting caused by reflecting light rays off of the voxel

	uint visible;          //whether or not the voxel was visible to the camera last frame
};

struct CompressedVoxel //a compressed voxel
{
	uint albedo;
	uint normal;
	uint directLight;
	uint specLight;
};

struct Chunk //a chunk of voxels
{
	CompressedVoxel voxels[CHUNK_SIZE.x][CHUNK_SIZE.y][CHUNK_SIZE.z]; //the voxels and chunks are separate so as to limit the amount of data that must be sent from the CPU
	uvec3 position; //the position of the chunk in the map, for streaming chunk data
	vec4 indirectLight[CHUNK_SIZE.x][CHUNK_SIZE.y][CHUNK_SIZE.z];     //not compressed due to a need for precision
};

struct ChunkHandle //a handle to a Chunk
{
	uint flag;     //0 = does not exist, 1 = exists and loaded, 2 = exists and unloaded, 3 = exists, unloaded, and requested
	uint visible;  //whether or not the chunk is visible to the camera
	uint lastUsed; //the time since the chunk was last used
	uint index;    //the index of the chunk in the chunk buffer
};

struct Material
{
	uint emissive;   //whether or not the voxel emits light

	float opacity;   //the opacity of the voxel

	float specular;  //what percentage of rays are specular, the other percent are diffuse
	uint reflectSky; //only applies to specular voxels. whether or not they will reflect the sky color
	uint shininess;  //the greater the number, the closer the specular rays are to perfect reflection
};

//--------------------------------------------------------------------------------------------------------------------------------//

layout(std140, binding = 0) restrict buffer mapBuffer //the buffer that contains the chunk map
{
	ChunkHandle map[];
};

layout(std140, binding = 1) restrict buffer chunkBuffer //the buffer that contains all of the chunks
{
	Chunk chunks[];
};

layout(std140, binding = 2) restrict readonly buffer materialBuffer //the buffer that contains all of the materials
{
	Material materials[256];
};

uniform vec3 sunStrength;

//--------------------------------------------------------------------------------------------------------------------------------//

uvec4 decode_uint_RGBA(uint val) //decompresses a 4-byte value into 4 1-byte values
{
	uvec4 res;
	res.x = val >> 24;
	res.y = val >> 16;
	res.z = val >> 8;
	res.w = val;
	return res & 0xFF;
}

uint encode_uint_RGBA(uvec4 val) //compresses 4 1-byte values into a 4-byte value
{
	val = val & 0xFF;
	return val.x << 24 | val.y << 16 | val.z << 8 | val.w;
}

//--------------------------------------------------------------------------------------------------------------------------------//

bool in_map_bounds(ivec3 pos) //returns whether a position is in bounds in the map
{
	return pos.x < mapSize.x && pos.y < mapSize.y && pos.z < mapSize.z && pos.x >= 0 && pos.y >= 0 && pos.z >= 0;
}

bool in_chunk_bounds(ivec3 pos) //returns whether a position is in bounds in a chunk
{
	return pos.x < CHUNK_SIZE.x && pos.y < CHUNK_SIZE.y && pos.z < CHUNK_SIZE.z && pos.x >= 0 && pos.y >= 0 && pos.z >= 0;
}

ChunkHandle get_map_tile(ivec3 pos) //returns the value of the map at a position
{
	uint index = pos.x + mapSize.x * (pos.y + mapSize.y * pos.z);
	map[index].lastUsed = 0;
	return map[index];
}

CompressedVoxel get_voxel(uint chunk, ivec3 pos) //returns the voxel of a chunk at a position
{
	return chunks[chunk].voxels[pos.x][pos.y][pos.z];
}

vec4 get_indirect_light(uint chunk, ivec3 pos) //returns the indirect light value of a voxel
{
	return chunks[chunk].indirectLight[pos.x][pos.y][pos.z];
}

bool does_voxel_exist(uint chunk, ivec3 localPos) //returns true if the voxel at the position is solid (not empty)
{
	CompressedVoxel voxel = get_voxel(chunk, localPos);
	uint material = voxel.albedo & 0xFF;
	return material < 255;
}

//--------------------------------------------------------------------------------------------------------------------------------//

Voxel decompress_voxel(CompressedVoxel compressed, vec4 indirectLight) //decompresses an entire voxel
{
	uvec4 albedo 		= decode_uint_RGBA(compressed.albedo);
	uvec4 normal 		= decode_uint_RGBA(compressed.normal);
	uvec4 directLight   = decode_uint_RGBA(compressed.directLight);
	uvec4 specLight 	= decode_uint_RGBA(compressed.specLight);

	Voxel res;
	res.albedo = albedo.xyz * 0.00392156862;
	res.normal = (normal.xyz * 0.00392156862 - 0.5) * 2.0;
	res.material = albedo.w;
	res.indirectLight = indirectLight.xyz;
	res.indirectSamples = indirectLight.w;
	res.directLight = directLight.xyz * 0.00392156862;
	res.specLight = specLight.xyz * 0.00392156862;
	res.visible = directLight.w;

	return res;
}

//--------------------------------------------------------------------------------------------------------------------------------//

vec2 intersect_AABB(vec3 invRayDir, vec3 rayPos, vec3 boxMin, vec3 boxMax) //returns how far along a ray a box is intersected
{
    vec3 tMin = (boxMin - rayPos) * invRayDir;
    vec3 tMax = (boxMax - rayPos) * invRayDir;

    vec3 t1 = min(tMin, tMax);
    vec3 t2 = max(tMin, tMax);

    float tNear = max(max(t1.x, t1.y), t1.z);
    float tFar  = min(min(t2.x, t2.y), t2.z);

    return vec2(tNear, tFar);
}

//--------------------------------------------------------------------------------------------------------------------------------//

vec3 sky_color(vec3 rayDir)
{
	//return mix(vec3(0.99, 0.51, 0.0), vec3(0.66, 0.55, 0.78), rayDir.y * 0.5 + 1); //sunset
	//return mix(vec3(0.02, 0.05, 0.2), vec3(0.00, 0.04, 0.18), rayDir.y * 0.5 + 1); //night
	return mix(vec3(0.71, 0.85, 0.90), vec3(0.00, 0.45, 0.74), rayDir.y * 0.5 + 1); //day
}

//--------------------------------------------------------------------------------------------------------------------------------//

void iterate_DDA(vec3 deltaDist, ivec3 rayStep, inout vec3 sideDist, inout ivec3 mapPos) //steps through one tile with the DDA algorithm
{
	//https://www.youtube.com/watch?v=NbSee-XM7WA <- see this video for a good overview of the algorithm

	if (sideDist.x < sideDist.y) 
	{
		if (sideDist.x < sideDist.z) 
		{
			sideDist.x += deltaDist.x;
			mapPos.x += rayStep.x;
		}
		else 
		{
			sideDist.z += deltaDist.z;
			mapPos.z += rayStep.z;
		}
	}
	else
	{
		if (sideDist.y < sideDist.z) 
		{
			sideDist.y += deltaDist.y;
			mapPos.y += rayStep.y;
		}
		else 
		{
			sideDist.z += deltaDist.z;
			mapPos.z += rayStep.z;
		}
	}
}

//--------------------------------------------------------------------------------------------------------------------------------//

uint lastVox = 255;

bool step_chunk(uint chunk, vec3 rayDir, vec3 invRayDir, vec3 rayPos, bool ignoreFirst, bool addSpec, out Voxel voxel, out ivec3 chunkPos, inout vec3 colorAdd, inout float colorMult) //steps a ray through a chunk, returns true if something was hit
{
	ivec3 mapPos = ivec3(floor(rayPos)); //the position in the voxel map
	vec3 deltaDist = abs(invRayDir);     //the distance the ray has to travel to move one unit in each direction
	ivec3 rayStep = ivec3(sign(rayDir)); //the direction the ray steps
	vec3 sideDist = (sign(rayDir) * (mapPos - rayPos) + (sign(rayDir) * 0.5) + 0.5) * deltaDist; //the total distance the ray has to travel to reach one additional unit in each direction (accounts for starting position as well)

	while(in_chunk_bounds(mapPos))
	{
		//check if a solid voxel has been hit:
		if (does_voxel_exist(chunk, mapPos) && !ignoreFirst)
		{
			//decompress the voxel and find its material:
			CompressedVoxel compressed = get_voxel(chunk, mapPos);
			voxel = decompress_voxel(compressed, get_indirect_light(chunk, mapPos));
			Material material = materials[voxel.material];

			if(material.opacity < 1.0)
			{
				//if the voxel is transparent, add its transparency multiplier (only if it is a "new" voxel)
				if(lastVox != compressed.albedo)
				{
					colorAdd += colorMult * material.opacity * voxel.albedo * sunStrength; //TODO: REFRACTION AND BEER'S LAW
					colorMult *= 1.0 - material.opacity;
					if(addSpec)
						colorAdd += voxel.specLight * material.specular;
					
					//set the visibility to true:
					//atomicExchange(chunks[chunk].voxels[mapPos.x][mapPos.y][mapPos.z].directLight, encode_uint_RGBA(uvec4(round(voxel.directLight * 255), 1)));
				}
			}
			else
			{
				//return the position and set the visibility of the voxel to true
				chunkPos = mapPos;
				//atomicExchange(chunks[chunk].voxels[mapPos.x][mapPos.y][mapPos.z].directLight, encode_uint_RGBA(uvec4(round(voxel.directLight * 255), 1)));
				return true;
			}

			lastVox = compressed.albedo;
		}
		else
			lastVox = 255;

		//iterate the dda algorithm:
		iterate_DDA(deltaDist, rayStep, sideDist, mapPos);
		ignoreFirst = false;
	}

	return false;
}

bool step_map(vec3 rayDir, vec3 invRayDir, inout vec3 rayPos, bool ignoreFirst, bool addSpec, out Voxel voxel, out vec3 colorAdd, out float colorMult) //steps a ray through the map (not individual chunks), returns true if something was hit
{
	colorAdd = vec3(0.0); //added to the final color to account for transparency
	colorMult = 1.0;      //multiplied with the final color to account for transparency

	ivec3 mapPos = ivec3(floor(rayPos)); //the position in the voxel map
	vec3 deltaDist = abs(invRayDir);     //the distance the ray has to travel to move one unit in each direction
	ivec3 rayStep = ivec3(sign(rayDir)); //the direction the ray steps
	vec3 sideDist = (sign(rayDir) * (mapPos - rayPos) + (sign(rayDir) * 0.5) + 0.5) * deltaDist; //the total distance the ray has to travel to reach one additional unit in each direction (accounts for starting position as well)
	vec3 updatedRayDir = (abs(rayDir) + EPSILON) * sign(rayDir); //to reduce noise

	while(in_map_bounds(mapPos))
	{
		//check if a solid chunk has been hit:
		ChunkHandle mapTile = get_map_tile(mapPos);
		if (mapTile.flag == 1)
		{
			vec2 intersection = intersect_AABB(invRayDir, rayPos, mapPos, mapPos + 1.0); //find exactly where the ray intersects
			if(intersection.x > 0) //only increment rayPos if outside the box
				rayPos += updatedRayDir * (intersection.x + EPSILON); //add so that new pos is inside the chunk

			//step through the chunk:
			ivec3 chunkPos;
			if(step_chunk(mapTile.index, rayDir, invRayDir, (rayPos - mapPos) * CHUNK_SIZE, ignoreFirst, addSpec, voxel, chunkPos, colorAdd, colorMult))
			{
				vec3 voxelPos = mapPos + chunkPos * INV_CHUNK_SIZE;
				intersection = intersect_AABB(invRayDir, rayPos, voxelPos, voxelPos + INV_CHUNK_SIZE); //find exactly where the ray intersects
				rayPos += rayDir * (intersection.x + EPSILON); //add so that new pos is inside the voxel
				map[mapPos.x + mapSize.x * (mapPos.y + mapSize.y * mapPos.z)].visible = 1;

				return true;
			}
		}
		else if(mapTile.flag > 1)
		{
			uint index = mapPos.x + mapSize.x * (mapPos.y + mapSize.y * mapPos.z);
			map[index].flag = 3;

			return true;
		}

		//iterate the dda algorithm:
		iterate_DDA(deltaDist, rayStep, sideDist, mapPos);
		ignoreFirst = false;
	}

	return false;
}

//-----------------------------------------------------------END-SHARED-----------------------------------------------------------//
