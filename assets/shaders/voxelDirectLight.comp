//INCLUDES "voxelShared.comp"
#version 430 core

layout(local_size_x = CHUNK_SIZE.x, local_size_y = CHUNK_SIZE.y, local_size_z = CHUNK_SIZE.z) in; //local group size of chunk

layout(std140, binding = 3) restrict readonly buffer lightingRequestBuffer
{
	ivec3 chunkPositions[];
};

//global sun params:
uniform vec3 sunDir; //must be normalized
uniform float shadowSoftness;
uniform float ambientStrength;

uniform vec3 camPos;

const vec3 spherePoints[15] = { vec3(0.000000, 1.000000, 0.000000), vec3(-0.379803, 0.857143, 0.347931), vec3(0.061185, 0.714286, -0.697174), vec3(0.499316, 0.571429, 0.651270), vec3(-0.889696, 0.428571, -0.157375), vec3(0.808584, 0.285714, -0.514354), vec3(-0.256942, 0.142857, 0.955810), vec3(-0.460906, 0.000000, -0.887449), vec3(0.929687, -0.142857, 0.339521), vec3(-0.885815, -0.285714, 0.365650), vec3(0.382949, -0.428571, -0.818338), vec3(0.245607, -0.571429, 0.783037), vec3(-0.605521, -0.714286, -0.350913), vec3(0.503065, -0.857143, -0.110596), vec3(-0.000000, -1.000000, 0.000000) };
//TODO: ALLOW MULTIPLE QUALITY OPTIONS FOR THE SPHERE POINTS

//--------------------------------------------------------------------------------------------------------------------------------//

bool shadow_ray(ivec3 chunkPos, ivec3 localPos, vec3 normal, vec3 rayPos, vec3 sunDir, out vec3 colorAdd, out float colorMult) //returns true if the shadow ray hit the map
{
	Voxel hitVoxel;

	return step_map(sunDir, 1 / sunDir, rayPos, true, false, hitVoxel, colorAdd, colorMult); //return whether or not the map was hit
}

void specular_ray(ivec3 chunkPos, ivec3 localPos, vec3 normal, vec3 rayPos, vec3 rayDir, vec3 albedo, uint reflectSky, inout vec3 color)
{
	Voxel hitVoxel;
	vec3 tempPos = rayPos;

	vec3 colorAdd;
	float colorMult;
	if(step_map(rayDir, 1 / rayDir, tempPos, true, false, hitVoxel, colorAdd, colorMult)) //TODO: ALLOW FOR MULTIPLE BOUNCES OF SPECULAR LIGHT
	{
		vec3 dist = abs(floor(tempPos * CHUNK_SIZE) - floor(rayPos * CHUNK_SIZE));
		if(dot(dist, dist) > 1)
		{
			Material hitMaterial = materials[hitVoxel.material];
			hitVoxel.specLight *= hitMaterial.specular;
			hitVoxel.indirectLight *= 1.0 - hitMaterial.specular;
			hitVoxel.directLight *= 1.0 - hitMaterial.specular;

			vec3 hitColor;
			if(hitMaterial.emissive)
				hitColor = hitVoxel.albedo;
			else
				hitColor = (hitVoxel.directLight + hitVoxel.indirectLight + ambientStrength) * hitVoxel.albedo;

			color += (hitColor * colorMult + colorAdd) * albedo;
		}
	}
	else if(dot(rayDir, sunDir) > 0.99)
		color += sunStrength * colorMult + colorAdd;
	else if(reflectSky == 1)
		color += sky_color(rayDir) * colorMult + colorAdd;
	else
		color += (sunStrength * colorMult + colorAdd) * max(dot(rayDir, sunDir), 0.1) * albedo;
}

//--------------------------------------------------------------------------------------------------------------------------------//

void main()
{
	//find positions:
	ivec3 chunkPos = chunkPositions[gl_WorkGroupID.x].xyz;
	ivec3 localPos = ivec3(gl_LocalInvocationID);
	int chunkIndex = get_map_tile(chunkPos);
	Voxel thisVoxel = decompress_voxel(get_voxel(chunkIndex, localPos), get_indirect_light(chunkIndex, localPos));
	Material thisMaterial = materials[thisVoxel.material];

	//return if voxel does not exist lol:
	if(thisVoxel.material == 255 || thisVoxel.visible == 0)
		return;
	
	//find position of ray (center of the voxel):
	vec3 rayPos = INV_CHUNK_SIZE * localPos + chunkPos + HALF_INV_CHUNK_SIZE;
	rayPos = rayPos + (HALF_INV_CHUNK_SIZE - vec3(EPSILON)) * thisVoxel.normal;

	//shadow rays:
	//---------------------------------
	vec3 directLight = vec3(0.0);
	for(int i = 0; i < 15; i++)
	{
		vec3 updatedSunDir = normalize(sunDir * shadowSoftness + spherePoints[i]);

		vec3 colorAdd;
		float colorMult;
		if(!shadow_ray(chunkPos, localPos, thisVoxel.normal, rayPos, updatedSunDir, colorAdd, colorMult))
			directLight += (sunStrength * colorMult + colorAdd) * colorMult; //TODO: maybe find a better way to do this
	}

	directLight /= 15.0;

	//specular rays:
	//---------------------------------
	vec3 specLight = vec3(0.0);
	vec3 viewDir = rayPos - camPos;
	if(thisMaterial.specular > 0.0 && dot(viewDir, thisVoxel.normal) < 0.0)
	{
		vec3 reflected = reflect(normalize(viewDir), thisVoxel.normal);

		for(int i = 0; i < 15; i++)
		{
			vec3 specDir = normalize(reflected * thisMaterial.shininess + spherePoints[i]);
			specular_ray(chunkPos, localPos, thisVoxel.normal, rayPos, specDir, thisVoxel.albedo, thisMaterial.reflectSky, specLight);
		}

		specLight /= 15.0;
	}

	//update the final color
	//---------------------------------
	directLight = clamp(directLight, vec3(0.0), vec3(1.0));
	specLight = clamp(specLight, vec3(0.0), vec3(1.0));

	atomicExchange(chunks[chunkIndex].voxels[localPos.x][localPos.y][localPos.z].directLight, encode_uint_RGBA(uvec4(round(directLight * 255), 0)));
	chunks[chunkIndex].voxels[localPos.x][localPos.y][localPos.z].specLight = encode_uint_RGBA(uvec4(round(specLight * 255), 0));

	return;
}