//INCLUDES "voxelShared.comp"
#version 430 core

//NOTE: can't use "CHUNK_SIZE" constant here because local_size must be a literal on some gpus
layout(local_size_x = 8, local_size_y = 8, local_size_z = 8) in; //local group size of chunk

layout(std140, binding = 3) restrict readonly buffer lightingRequestBuffer //a buffer that holds all of the chunks that are set to have their lighting updated
{
	uint chunkIndices[];
};

uniform float time; //for random seeding
uniform uint numDiffuseSamples;   //the number of diffuse lighting samples to take
uniform uint maxDiffuseSamples;   //the maximum number of diffuse samples that can be stored
uniform uint diffuseBounceLimit;  //the maximum number of times a diffuse light ray can bounce
uniform uint specularBounceLimit; //the maximum number of times a specular light ray can bounce

uniform vec3 sunDir;           //the vector pointing towards the sun, must be normalized
uniform float shadowSoftness;  //how soft the shadows appear

uniform vec3 camPos; //the position of the camera

//a set of points along the unit sphere, sampled for soft shadows and rough reflections:
const vec3 spherePoints[15] = { vec3(0.000000, 1.000000, 0.000000), vec3(-0.379803, 0.857143, 0.347931), vec3(0.061185, 0.714286, -0.697174), vec3(0.499316, 0.571429, 0.651270), vec3(-0.889696, 0.428571, -0.157375), vec3(0.808584, 0.285714, -0.514354), vec3(-0.256942, 0.142857, 0.955810), vec3(-0.460906, 0.000000, -0.887449), vec3(0.929687, -0.142857, 0.339521), vec3(-0.885815, -0.285714, 0.365650), vec3(0.382949, -0.428571, -0.818338), vec3(0.245607, -0.571429, 0.783037), vec3(-0.605521, -0.714286, -0.350913), vec3(0.503065, -0.857143, -0.110596), vec3(-0.000000, -1.000000, 0.000000) };
//TODO: ALLOW MULTIPLE QUALITY OPTIONS FOR THE SPHERE POINTS

//--------------------------------------------------------------------------------------------------------------------------------//

float rand(float seed) //returns a random float between -1 and 1
{
    return fract(sin(seed) * 43758.5453) * 2.0 - 1.0;
}

vec2 rand2(float seed) //returns a vec2 of random floats between -1 and 1
{
	return vec2(rand(seed), rand(seed * 2.0));
}

vec3 rand3(float seed) //returns a vec3 of random floats between -1 and 1
{
	return vec3(rand(seed), rand(seed * 2.0), rand(seed * 3.0));
}

vec3 rand_unit_sphere(float seed) //returns a vec3 of random floats between -1 and 1 that is inside the unit sphere (mag < 1) 
{
	while(true)
	{
		vec3 point = rand3(seed++);
		if(dot(point, point) >= 1.0)
			continue;

		return point;
	}
}

//--------------------------------------------------------------------------------------------------------------------------------//

void shadow_ray(vec3 rayPos, float seed, inout vec3 color) //returns true if the shadow ray hit the map
{
	Voxel hitVoxel; //not used
	vec3 updatedSunDir = normalize(sunDir * shadowSoftness + rand_unit_sphere(seed));
	vec3 colorAdd;
	float colorMult;

	if(!step_map(updatedSunDir, 1 / updatedSunDir, rayPos, true, hitVoxel, colorAdd, colorMult))
		color += sunStrength * colorMult + colorAdd;
}

void specular_ray(ivec3 mapPos, vec3 normal, vec3 rayPos, vec3 rayDir, vec3 albedo, uint reflectType, inout vec3 color) //casts out a specular ray and adds the appropriate color to color
{
	Voxel hitVoxel;
	Material hitMaterial;

	vec3 lastPos = rayPos;    //the previous ray position
	vec3 multiplier = albedo; //all of the hit albedos multiplied together

	for(int i = 0; i < specularBounceLimit; i++)
	{
		vec3 colorAdd;
		float colorMult;

		//check if a voxel was hit:
		if(step_map(rayDir, 1 / rayDir, rayPos, true, hitVoxel, colorAdd, colorMult))
		{
			//get position:
			ivec3 hitMapPos = ivec3(rayPos);
			uint thisMapIndex = mapPos.x    + mapSize.x * (mapPos.y    + mapSize.y * mapPos.z   );
			uint hitMapIndex  = hitMapPos.x + mapSize.x * (hitMapPos.y + mapSize.y * hitMapPos.z);
			if(map[thisMapIndex].visible == 1 && map[hitMapIndex].visible == 0)
				map[hitMapIndex].visible = 1;

			//check if hit voxel is adjacent (meant that this voxel is occluded):
			vec3 dist = abs(floor(rayPos * CHUNK_SIZE) - floor(lastPos * CHUNK_SIZE));
			if(dot(dist, dist) > 1)
			{
				hitMaterial = materials[hitVoxel.material];
				hitVoxel.diffuseLight *= 1.0 - hitMaterial.specular;

				if(hitMaterial.emissive)
				{
					color += (hitMaterial.albedo * colorMult + colorAdd) * multiplier * albedo;
					break;
				}
				else
				{
					vec3 hitColor = (ambientStrength) * hitMaterial.albedo + hitVoxel.diffuseLight;	
					color += (hitColor * colorMult + colorAdd) * multiplier;

					//reflect ray again if the hit voxel is specular:
					if(hitMaterial.specular > 0.0)
					{
						multiplier *= hitMaterial.albedo * colorMult * hitMaterial.specular;
						reflectType = hitMaterial.reflectType;
						lastPos = rayPos;
						rayDir = reflect(rayDir, hitVoxel.normal);
					}
					else
						break;
				}
			}
			else
				break;
		}
		else if(dot(rayDir, sunDir) > 0.99) //add specular highlight
		{
			color += (sunStrength * colorMult + colorAdd);
			break;
		}
		else //reflect the sky color
		{
			color += ((reflectType == 1 ? sky_color(rayDir) : sunStrength) * colorMult + colorAdd) * multiplier;
			break;
		}
	}
}

void diffuse_ray(vec3 normal, vec3 rayPos, Voxel initialVoxel, float seed, inout vec3 color) //increments color and numSamples depending on the voxel hit
{
	Voxel hitVoxel = initialVoxel; //stores the voxel that the ray hits
	vec3 hitNormal = normal;       //stores the normal of the hit voxel
	Material hitMaterial;

	vec3 newColor = vec3(1.0);

	vec3 lastPos = rayPos;
	vec3 lastDir;
	for(int i = 0; i < diffuseBounceLimit; i++)
	{
		vec3 dir;
		if(i > 0 && (rand(seed + diffuseBounceLimit + i) + 1) * 0.5 < hitMaterial.specular)
			dir = normalize(reflect(lastDir, hitNormal) * hitMaterial.shininess + rand_unit_sphere(time + i)); //TODO: check if this actually works i cant tell with my current example scene
		else
			dir = normalize(hitNormal + rand_unit_sphere(seed + i)); //randomize the direction, spherical sampling
		
		vec3 colorAdd;
		float colorMult;

		bool hit = step_map(dir, 1 / dir, rayPos, true, hitVoxel, colorAdd, colorMult);  //step through map
		
		hitNormal = hitVoxel.normal;
		hitMaterial = materials[hitVoxel.material];

		if(hit)
		{
			//return if you hit an adjacent voxel (means that this voxel is occluded):
			vec3 dist = abs(floor(lastPos * CHUNK_SIZE) - floor(rayPos * CHUNK_SIZE));
			if(dot(dist, dist) < 1)
				return;

			//multiply the final color:
			if(hitMaterial.emissive)
			{
				color += newColor * (hitMaterial.albedo * colorMult + colorAdd);
				return;
			}
			else
				newColor *= (hitMaterial.albedo * colorMult + colorAdd);
		}
		else
		{
			color += (newColor * max(dot(dir, sunDir), 0.0) * sunStrength * colorMult + colorAdd);
			return;
		}

		lastDir = dir;
	}
}

//--------------------------------------------------------------------------------------------------------------------------------//

void main()
{
	//find positions:
	uint chunkIndex = chunkIndices[gl_WorkGroupID.x];
	ivec3 mapPos = ivec3(chunks[chunkIndex].pos.xyz);
	ivec3 chunkPos = ivec3(gl_LocalInvocationID);
	Voxel thisVoxel = decompress_voxel(get_voxel(chunkIndex, chunkPos));
	Material thisMaterial = materials[thisVoxel.material];
	float indirectSamples = float(min(chunks[chunkIndex].numIndirectSamples, maxDiffuseSamples)); //have a maximum number of samples to allow the lighting to change quicker

	//increase number of diffuse samples for newly updated chunks to avoid noise:
	uint updatedDiffuseSamples = numDiffuseSamples;
	if(indirectSamples == 0)
		updatedDiffuseSamples += 50;
	
	//find position of ray (center of the voxel):
	vec3 rayPos = INV_CHUNK_SIZE * chunkPos + mapPos + HALF_INV_CHUNK_SIZE;
	rayPos = rayPos + (HALF_INV_CHUNK_SIZE - vec3(EPSILON)) * thisVoxel.normal;

	//lighting types:
	vec3 specLight = vec3(0.0);
	vec3 diffuseLight = vec3(0.0);

	//specular rays:
	vec3 viewDir = rayPos - camPos; //the vector from the ray position to the camera
	if(thisMaterial.specular > 0.0 && dot(viewDir, thisVoxel.normal) < 0.0 && thisMaterial.reflectType <= 1)
	{
		vec3 reflected = reflect(normalize(viewDir), thisVoxel.normal);

		for(int i = 0; i < 15; i++)
		{
			vec3 specDir = normalize(reflected * thisMaterial.shininess + spherePoints[i]);
			specular_ray(mapPos, thisVoxel.normal, rayPos, specDir, thisMaterial.albedo, thisMaterial.reflectType, specLight);
		}

		specLight /= 15.0;
	}

	//diffuse and shadow rays:
	if(thisMaterial.specular < 1.0)
	{
		for(int i = 0; i < updatedDiffuseSamples; i++)
		{
			diffuse_ray(thisVoxel.normal, rayPos, thisVoxel, time * (i + 1), diffuseLight);
			shadow_ray(rayPos, time * (i + 1 + updatedDiffuseSamples), diffuseLight);
		}

		diffuseLight *= thisMaterial.albedo;
		diffuseLight = (thisVoxel.diffuseLight * indirectSamples + diffuseLight) / (indirectSamples + updatedDiffuseSamples); //add to average
	}

	//clamp to avoid compression errors:
	specLight 	 = clamp(specLight,    vec3(0.0), vec3(1.0));
	diffuseLight = clamp(diffuseLight, vec3(0.0), vec3(1.0));

	//determine how to write diffuse:
	uvec3 writeDiffuseLight = uvec3(round(diffuseLight * 65535));
	uvec3 writeLower = writeDiffuseLight & 0xFF;
	uvec3 writeUpper = (writeDiffuseLight >> 8) & 0xFF;

	//store lighting:
	atomicExchange(chunks[chunkIndex].voxels[chunkPos.x][chunkPos.y][chunkPos.z].directLight,   encode_uint_RGBA(uvec4(round(thisMaterial.albedo * 255), round(specLight.x * 255))));
	atomicExchange(chunks[chunkIndex].voxels[chunkPos.x][chunkPos.y][chunkPos.z].specLight,     encode_uint_RGBA(uvec4(round(specLight.yz * 255), writeUpper.x, writeLower.x)));
	atomicExchange(chunks[chunkIndex].voxels[chunkPos.x][chunkPos.y][chunkPos.z].diffuseLight,  encode_uint_RGBA(uvec4(writeUpper.y, writeLower.y, writeUpper.z, writeLower.z)));

	//increase number of samples:
	if(chunkPos == ivec3(0, 0, 0))
		atomicAdd(chunks[chunkIndex].numIndirectSamples, updatedDiffuseSamples);

	//set visible to false:
	uint mapIndex = mapPos.x + mapSize.x * (mapPos.y + mapSize.y * mapPos.z);
	map[mapIndex].visible = 0;

	return;
}